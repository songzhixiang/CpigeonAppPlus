apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileOptions.encoding = "UTF-8"
    updateVersionConfig()
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    signingConfigs {
        //签名设置
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }
    defaultConfig {
        multiDexEnabled true
        applicationId "com.cpigeon.app"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode getCustomVersionCode()
        versionName getCustomVersionName()
        signingConfig signingConfigs.release //默认签名配置
        vectorDrawables.useSupportLibrary = true
        //testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SERVER", "\"http://www.cpigeon.com:818\"" //默认服务器地址配置
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "1040749beca7f03dc6211519", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
    //修改生成的apk名字
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def oldFile = output.outputFile
            def newName = '';
            if (variant.buildType.name.equals('release')) {
//                println(variant.productFlavors[0].name)
                //def releaseApkName = 'app-release-v' + getCustomVersionName() + '-' + variant.productFlavors[0].name + '-' + getCustomVersionCode() + '.apk'
                newName = oldFile.name.replace(".apk", "-v" + getCustomVersionName() + "-" + getCustomVersionCode() + ".apk")
                output.outputFile = new File(oldFile.parent, newName)
            }
            if (variant.buildType.name.equals('beta')) {
                newName = oldFile.name.replace(".apk", "-v" + getCustomVersionName() + "-beta" + getDate() + ".apk")
                output.outputFile = new File(oldFile.parent, newName)
            }
            if (variant.buildType.name.equals('debug')) {
            }
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
//            signingConfig signingConfigs.release
        }
        debug {
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            //混淆
//            minifyEnabled true
//            // 移除无用的resource文件
//            shrinkResources true
            buildConfigField "String", "SERVER", "\"http://192.168.0.52:8888\"" //配置BuildConfig字段
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        cpigeon {
            //中鸽官网
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }
//    compileOptions {
//        sourceCompatibility JavaVersion.VERSION_1_7
//        targetCompatibility JavaVersion.VERSION_1_7
//    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile files('libs/commons-codec-1.10.jar')
    compile files('libs/libammsdk.jar')
    compile 'com.android.support:appcompat-v7:25.3.0'
    //Gson
    compile 'com.google.code.gson:gson:2.8.0'
    //权限管理
    compile 'com.github.hotchemi:permissionsdispatcher:2.3.2'
    //视图注解
    compile 'com.jakewharton:butterknife:8.5.1'
    //视频播放
    compile 'fm.jiecao:jiecaovideoplayer:5.5.2'
    //图片加载
    compile 'com.squareup.picasso:picasso:2.5.2'
    //底部TabBar
    compile 'com.ashokvarma.android:bottom-navigation-bar:1.4.1'
    compile 'com.roughike:bottom-bar:2.3.1'
    //dex分包兼容5.0
    compile 'com.android.support:multidex:1.0.1'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:gridlayout-v7:25.3.0'
    compile 'com.youth.banner:banner:1.4.9'
    compile 'org.xutils:xutils:3.5.0'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.8'
    //其中lat est.release指代最新版本号，也可以指定明确的版本号，例如2.1.5
    compile 'com.tencent.bugly:crashreport:latest.release'
    //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0
    compile 'com.tencent.bugly:nativecrashreport:latest.release'
    compile 'cn.bingoogolapple:bga-badgeview:1.1.3'
    compile 'cn.pedant.sweetalert:library:1.3'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.kyleduo.switchbutton:library:1.4.5'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.3.2'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    // 可滑动结束Activity的布局
    compile 'me.imid.swipebacklayout.lib:library:1.0.0'
    // 此处以JPush 3.0.3 版本为例。
    compile 'cn.jiguang.sdk:jpush:3.0.3'
    // 此处以JCore 1.1.1 版本为例。
    compile 'cn.jiguang.sdk:jcore:1.1.1'
    compile files('libs/nineoldandroids-2.4.0.jar')
    compile 'com.alibaba:fastjson:1.2.31'
}

int getCustomVersionCode() {
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        return versionProps['VERSION_CODE'].toInteger()
    }
}

def getCustomVersionName() {
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def name = versionProps['VERSION_NAME']
        return name
    }
}

def updateVersionConfig() {
    //如果version.properties文件可读则执行操作
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        //载入version.properties
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def buildTime = new Date()
        //上次次数重置时间与当前时间作对比
        Date lastResetBuildTime = new Date(versionProps['BUILD_TIMES_RESET_TIME'].toLong())
//        println lastResetBuildTime.year + "/" + lastResetBuildTime.month + "/" + lastResetBuildTime.day
//        println buildTime.year + "/" + buildTime.month + "/" + buildTime.day
        if (lastResetBuildTime.year != buildTime.year || lastResetBuildTime.month != buildTime.month || lastResetBuildTime.day != buildTime.day) {
            //如果重置时间不是当天的日期，则重置次数
            println '重置发布次数'
            versionProps['BUILD_TIMES'] = 0.toString()
            versionProps['BUILD_TIMES_RESET_TIME'] = buildTime.time.toString()
        }
        //写入编译时间
        versionProps['BUILD_LAST_TIME'] = buildTime.time.toString()
        //写入版本代码
        versionProps['VERSION_CODE'] = new Date().format("yyyyMMdd") + versionProps['BUILD_TIMES'].toString()

        println(versionProps)
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

//获取时间戳
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}
